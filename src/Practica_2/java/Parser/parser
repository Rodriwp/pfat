package Parser;

import java_cup.runtime.*;
import AST.*;

parser code {:
  public void syntax_error(Symbol s) {
    report_error("Error de sintaxis en linea " + s.left, null);
  }

  public void unrecovered_syntax_error(Symbol s) throws java.lang.Exception {
    report_fatal_error("", null);
  }
:};

terminal PC, ASOP, IF, THEN, ELSE, ENDIF, WHILE, PRINT, INT2STR, STR2INT,BREAK,NOT, MENORQUE, MAS, MENOS, POR, DIV, AND, OR, IGUALQUE, MAYORQUE, PAREN, TESIS, PROG, ABRELLAVE, CIERRALLAVE, COMA, VARS,OPUESTO;
terminal Integer CENT, TIPO;
terminal Boolean CLOG;
terminal String IDENT, CST;

non terminal S S;
non terminal LVar LVar;
non terminal Sentencia Sent,SentSimp,SentList,Body;
non terminal Asign Asign;
non terminal Cond Cond;
non terminal Repet Repet;
non terminal Print Print;
non terminal VDef VDef;
non terminal Decl Decl;
non terminal Exp Exp;

precedence right OR;
precedence right AND;
precedence right NOT;
precedence right IGUALQUE;
precedence right MENORQUE, MAYORQUE;
precedence right MAS, MENOS;
precedence right POR, DIV;
precedence right OPUESTO;

start with S;

S ::= PROG IDENT:ident PAREN LVar:lvar TESIS Body:body                  {: RESULT = new S1(ident, lvar, body); :}
  |   PROG IDENT:ident Body:body                                        {: RESULT = new S2(ident, body); :}
  |   PROG IDENT:ident VARS VDef:vdef Body:body                         {: RESULT = new S3(ident, vdef, body); :}
  |   PROG IDENT:ident PAREN LVar:lvar TESIS VARS VDef:vdef Body:body   {: RESULT = new S4(ident, lvar, vdef, body); :}
  ;

VDef ::=  Decl:decl PC            {: RESULT = new VDef1(decl); :}
      |   Decl:decl PC VDef:vdef  {: RESULT = new VDef2(decl, vdef); :}
      ;

Decl ::= TIPO:tipo LVar:lvar  {: RESULT = new Decl(tipo, lvar); :};

LVar ::=  IDENT:ident                 {: RESULT = new LVar1(ident); :}
      |   IDENT:ident COMA LVar:lvar  {: RESULT = new LVar2(ident, lvar); :}
      ;

Body ::= Sent:sent  {: RESULT = new Body(sent); :};

Sent ::=  SentSimp:sentsimp                         {: RESULT = new Sent1(sentsimp); :}
      |   ABRELLAVE SentList:sentlist CIERRALLAVE   {: RESULT = new Sent2(sentlist); :}
      ;

SentList ::=  SentSimp:sentsimp                     {: RESULT = new SentList1(sentsimp); :}
          |   SentSimp:sentsimp SentList:sentlist   {: RESULT = new SentList2(sentsimp, sentlist); :}
          ;

SentSimp ::= Asign:asign PC     {: RESULT = new SentSimp1(asign); :}
          |  Cond:cond          {: RESULT = new SentSimp2(cond); :}
          |  Repet:repet        {: RESULT = new SentSimp3(repet); :}
          |  Print:print PC     {: RESULT = new SentSimp4(print); :}
          |  BREAK CENT:cent PC {: RESULT = new SentSimp5(cent); :}
          ;

Print ::= PRINT PAREN Exp:exp TESIS {: RESULT = new Print(exp); :};

Asign ::= IDENT:ident ASOP Exp:exp {: RESULT = new Asign(ident, exp); :};

Cond ::=  IF Exp:exp THEN Sent:sent ENDIF                                     {: RESULT = new Cond1(exp, sent); :}
      |   IF Exp:exp THEN Sent:sent1 ELSE Sent:sent2 ENDIF                    {: RESULT = new Cond2(exp, sent1, sent2); :}
      ;

Repet ::= WHILE Exp:exp Sent:sent {: RESULT = new Repet(exp, sent); :};

Exp ::= IDENT:ident                 {: RESULT = new ExpIdent(ident); :}
    |   PAREN Exp:exp TESIS         {: RESULT = new ExpParentesis(exp); :}
    |   CENT:ent                    {: RESULT = new ExpEntero(ent); :}
    |   Exp:exp1 MAS Exp:exp2       {: RESULT = new ExpSuma(exp1, exp2); :}
    |   Exp:exp1 MENOS Exp:exp2     {: RESULT = new ExpResta(exp1, exp2); :}
    |   Exp:exp1 POR Exp:exp2       {: RESULT = new ExpMultiplicacion(exp1, exp2); :}
    |   Exp:exp1 DIV Exp:exp2       {: RESULT = new ExpDivision(exp1, exp2); :}
    |   MENOS Exp:exp               {: RESULT = new ExpOpuesto(exp); :} %prec OPUESTO
    |   STR2INT PAREN Exp:exp TESIS {: RESULT = new ExpStr2Int(exp); :}
    |   CST:str                     {: RESULT = new ExpString(str); :}
    |   INT2STR PAREN Exp:exp TESIS {: RESULT = new ExpInt2Str(exp); :}
    |   CLOG:bool                   {: RESULT = new ExpBoolean(bool.booleanValue()); :}
    |   Exp:exp1 OR Exp:exp2        {: RESULT = new ExpOr(exp1, exp2); :}
    |   Exp:exp1 AND Exp:exp2       {: RESULT = new ExpAnd(exp1, exp2); :}
    |   NOT Exp:exp                 {: RESULT = new ExpNot(exp); :}
    |   Exp:exp1 IGUALQUE Exp:exp2  {: RESULT = new ExpIgual(exp1, exp2); :}
    |   Exp:exp1 MENORQUE Exp:exp2  {: RESULT = new ExpMenor(exp1, exp2); :}
    |   Exp:exp1 MAYORQUE Exp:exp2  {: RESULT = new ExpMayor(exp1, exp2); :}
;
